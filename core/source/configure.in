AC_REVISION($Revision$)
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(config)

# checking for the GLOBUS_LOCATION

if test "x$GLOBUS_LOCATION" = "x"; then
    echo "ERROR Please specify GLOBUS_LOCATION" >&2
    exit 1
fi

if test "x$GPT_LOCATION" = "x"; then
    GPT_LOCATION=$GLOBUS_LOCATION
    export GPT_LOCATION
fi

prefix='$(GLOBUS_LOCATION)'
exec_prefix='$(GLOBUS_LOCATION)'

# Call init macros

GPT_INIT
AM_INIT_AUTOMAKE($GPT_NAME, $GPT_VERSION,0)
AM_CONFIG_HEADER(globus_config.h)

AC_ARG_WITH(
    flavor,
    [  --with-flavor                 Globus flavor string],
    [globus_cv_flavor="$withval"],
    [globus_cv_flavor="notset"])


if test "$globus_cv_flavor" = "notset" \
     -o "$globus_cv_flavor" = "no" \
     -o "$globus_cv_flavor" = "yes" ; then
    AC_MSG_ERROR([A flavor description string must be specified!])
fi

GLOBUS_FLAVOR_NAME=$globus_cv_flavor
AC_SUBST(GLOBUS_FLAVOR_NAME)

dnl define FILELIST_FILE variable
FILELIST_FILE=`pwd`;
FILELIST_FILE="$FILELIST_FILE/pkgdata/master.filelist"
AC_SUBST(FILELIST_FILE)

LAC_COMPILERS

AC_ARG_ENABLE(internal-doc,
[  --enable-am-depends		Enable automake dependency caching.
				(Requires gcc, developers only)],
[
	if test "X$GCC" = "Xyes"; then
		AUTOMAKE_OPTS=""
	else
		AUTOMAKE_OPTS="no-dependencies"
	fi
],
[
	AUTOMAKE_OPTS="no-dependencies"
])

AC_SUBST(AUTOMAKE_OPTS)

LAC_TARGET_ARCH

save_LD=$LD
LD=
AM_PROG_LIBTOOL
mv ./libtool libtool-$GLOBUS_FLAVOR_NAME
LD=$save_LD

dnl
dnl Output selected thread package
dnl
GLOBUS_THREADS=$lac_cv_threads_type
if test "$GLOBUS_THREADS" = "no" ; then
    GLOBUS_THREADS=none
fi

AC_SUBST(GLOBUS_THREADS)

dnl Automake is using these for executables somehow
AC_OBJEXT
AC_EXEEXT


AC_OUTPUT(Makefile
       	scripts/globus-build-env-$GLOBUS_FLAVOR_NAME.sh:scripts/globus-build-env.sh.in
	scripts/Makefile
	aclocal/Makefile
	aclocal/automake_config
	config/Makefile
	doxygen/Makefile
	pkgdata/Makefile
	pkgdata/pkg_data_src.gpt
	,
	$GPT_LOCATION/sbin/gpt_generate_bin_pkg_data  \
	--flavor=$GLOBUS_FLAVOR_NAME "./pkgdata/pkg_data_src.gpt"
	,
	GLOBUS_FLAVOR_NAME=$GLOBUS_FLAVOR_NAME
	GPT_LOCATION=$GPT_LOCATION
	)





